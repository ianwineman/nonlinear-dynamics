using Plots

function simple_climate_model_f(T; p=[0.65])
	ϵ = p[1]
	0.5 + 0.2*tanh((T - 263)/4) - ϵ*10*(0.002*T)^4
end

function simple_climate_model_J(T; p=[0.65])
	ϵ = p[1]
	(sech((T - 263)/4)^2)/20 - 0.08*ϵ*(0.002T)^3
end

function simple_climate_model_J_inv(T; p=[0.65])
	ϵ = p[1]
	20 / (sech((T - 263)/4)^2 - 1.6*ϵ*(0.002T)^3)
end

function newtons_method(f, x0, J_inv; p=[], atol=0.001)
	xj = x0
	while abs(f(xj; p=p)) > atol
		f_xj = f(xj; p=p)
		next_xj = xj - J_inv(xj; p=p)*f_xj
		xj = next_xj
	end
	return xj
end

let # Graph Bifurcation diagram generated by dense sampling with coloring based on stability
	stable_fixed_points = []
	unstable_fixed_points = []
	for ϵ in 0.3:0.005:0.9
		for T0 in 200:350
			Tstar = newtons_method(simple_climate_model_f, T0, simple_climate_model_J_inv; p=[ϵ])
			if simple_climate_model_J(Tstar; p=[ϵ]) < 0.0
				push!(stable_fixed_points, (ϵ,Tstar))
			else
				push!(unstable_fixed_points, (ϵ,Tstar))
			end
		end
	end

	scatter(
		first.(stable_fixed_points), last.(stable_fixed_points), 
		xlim=[0.3,0.9], ylim=[200,350],
		xlabel="ϵ (effective emissivity)", ylabel="\$T^*\$",
		label="Stable",
		msw=0, ms=3, mc=:blue,
		title="Simple climate model\nBifurcation Diagram (dense sampling)"
	)
	scatter!(
		first.(unstable_fixed_points), last.(unstable_fixed_points), 
		label="Unstable",
		msw=0, ms=3, mc=:red
	)
	savefig("plots/exercise4_2_1.png")
end

function continuation(f, zm1, zm0, N, J_inv)
	# z = [p,X]
	zm, zm_min1 = zm1, zm0
	zms = [zm, zm_min1]
	for m in 1:N
		ẑ = zm .+ (zm .- zm_min1)                                             # predictor (secant)
		zm_pls1 = [first(ẑ), newtons_method(f, last(ẑ), J_inv; p=[first(ẑ)])] # corrector (Newton's method)
		push!(zms, zm_pls1)
		zm_min1 = zm
		zm = zm_pls1
	end
	return zms
end

let # Graph Bifurcation diagram generated by continuation with coloring based on continuation
	c1 = continuation(simple_climate_model_f, [0.895, 213.95937857103337], [0.9, 213.65831389977643], 120, simple_climate_model_J_inv)
	#println(last(c1))
	scatter(
		first.(c1), last.(c1), 
		xlim=[0.3,0.9], ylim=[200,350],
		xlabel="ϵ (effective emissivity)", ylabel="\$T^*\$",
		msw=0, ms=3, mc=:blue,
		label="continuation 1",
		title="Simple climate model\nBifurcation Diagram (continuation)"
	)

	c2 = continuation(simple_climate_model_f, [0.305, 346.0758460497819], [0.3, 347.5092686882591], 120, simple_climate_model_J_inv)
	#println(last(c2))
	scatter!(first.(c2), last.(c2), mc=:purple, label="continuation 2", msw=0, ms=3)

	c3 = continuation(simple_climate_model_f, [0.505, 259.84539137448337], [0.5, 259.6947152012114], 80, simple_climate_model_J_inv)
	#println(last(c3))
	scatter!(first.(c3), last.(c3), mc=:green, label="continuation 3", msw=0, ms=3)

	c4 = continuation(simple_climate_model_f, [0.495, 259.5447376335643], [0.5, 259.6947152012114], 30, simple_climate_model_J_inv)
	#println(last(c4))
	scatter!(first.(c4), last.(c4), mc=:orange, label="continuation 4", msw=0, ms=3)
	savefig("plots/exercise4_2_2.png")
end

let # Graph Bifurcation diagram generated by continuation with coloring based on stability
	stable_fixed_points = []
	unstable_fixed_points = []

	c1 = continuation(simple_climate_model_f, [0.895, 213.95937857103337], [0.9, 213.65831389977643], 120, simple_climate_model_J_inv)
	c2 = continuation(simple_climate_model_f, [0.305, 346.0758460497819], [0.3, 347.5092686882591], 120, simple_climate_model_J_inv)
	c3 = continuation(simple_climate_model_f, [0.505, 259.84539137448337], [0.5, 259.6947152012114], 80, simple_climate_model_J_inv)
	c4 = continuation(simple_climate_model_f, [0.495, 259.5447376335643], [0.5, 259.6947152012114], 30, simple_climate_model_J_inv)

	for fixed_point in vcat(c1,c2,c3,c4)
		ϵ, T = fixed_point
		if simple_climate_model_J(T; p=[ϵ]) < 0.0
			push!(stable_fixed_points, (ϵ,T))
		else
			push!(unstable_fixed_points, (ϵ,T))
		end
	end

	scatter(
		first.(stable_fixed_points), last.(stable_fixed_points), 
		xlim=[0.3,0.9], ylim=[200,350],
		xlabel="ϵ (effective emissivity)", ylabel="\$T^*\$",
		label="Stable",
		msw=0, ms=3, mc=:blue,
		title="Simple climate model\nBifurcation Diagram (continuation)"
	)
	scatter!(
		first.(unstable_fixed_points), last.(unstable_fixed_points), 
		label="Unstable",
		msw=0, ms=3, mc=:red
	)
	savefig("plots/exercise4_2_3.png")
end
