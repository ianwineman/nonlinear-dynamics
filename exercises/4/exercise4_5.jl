using LinearAlgebra, Plots

function model_f(X; params=[1.0, 1.2, 0.8, 0.8])
	p, q, r, s = params
	x, y, z = X
	return [-x - y, -p*z + r*y + s*z^2 -y*z^2, -q*(x+z)]
end

function model_J(X; params=[1.0, 1.2, 0.8, 0.8])
	p, q, r, s = params
	x, y, z = X
	return [-1.0 -1.0 0.0; 0.0 (r-z^2) (-p + 2*s*z - 2*y*z); -q 0.0 -q]
end

function model_J_inv(X; params=[1.0, 1.2, 0.8, 0.8])
	inv(model_J(X; params=params))
end

function damped_newtons_method(f, X0, J_inv; params=[], atol=0.001)
	Xj = X0
	while norm(f(Xj; params=params)) > atol
		δ = 1.0
		f_Xj = f(Xj; params=params)

		while norm(f(Xj .- δ .* J_inv(Xj; params=params)*f_Xj; params=params)) >= norm(f_Xj)
			δ /= 2
			if δ < atol
				break
			end
		end

		next_Xj = Xj .- δ .* J_inv(Xj; params=params) * f_Xj
		Xj = next_Xj
	end
	return Xj
end

function continuation(f, zm1, zm0, N, J_inv; params=[], i=1)
	# z = [p,X]
	zm, zm_min1 = zm1, zm0
	zms = [zm, zm_min1]
	for m in 1:N
		ẑ = zm .+ (zm .- zm_min1)                                                     # predictor (secant)
		p = params
		p[i] = first(ẑ)
		zm_pls1 = vcat(first(ẑ), damped_newtons_method(f, ẑ[2:end], J_inv; params=p)) # corrector (Newton's method)
		push!(zms, zm_pls1)
		zm_min1 = zm
		zm = zm_pls1
	end
	return zms
end

let # Graph Bifurcation diagram generated by continuation with coloring based on continuation
	stable_fixed_points = []
	unstable_fixed_points = []

	c1 = continuation(
		model_f, 
		[1.99, 0.00018087502849328113, -0.00018087502849328113, -0.00018087502849328113], 
		[2.00, 0.00016873564499889603, -0.00016873564499889603, -0.00016873564499889603], 
		200, 
		model_J_inv 
		; params=[1.0, 1.2, 0.8, 0.8]
	)
	c2 = continuation(
		model_f, 
		[0.005, -1.3772452813705665, 1.3772452813705665, 1.3772452813705665], 
		[0.000, -1.3800511333481853, 1.3800511333481853, 1.3800511333481853], 
		200, 
		model_J_inv 
		; params=[1.0, 1.2, 0.8, 0.8]
	)
	c3 = continuation(
		model_f, 
		[0.01, 0.5746948781478864, -0.5746948781478864, -0.5746948781478864], 
		[0.00, 0.5798090087074494, -0.5798090087074494, -0.5798090087074494], 
		200, 
		model_J_inv 
		; params=[1.0, 1.2, 0.8, 0.8]
	)

	for fixed_point in vcat(c1,c2,c3)
		p, x, y, z = fixed_point
		J = model_J([x, y, z]; params=[p, 1.2, 0.8, 0.8])
		if all(x->x<0.0, real.(eigen(J).values))
			push!(stable_fixed_points, (p,x))
		else
			push!(unstable_fixed_points, (p,x))
		end
	end

	scatter(
		first.(stable_fixed_points), getindex.(stable_fixed_points, 2), 
		xlim=[0,2],
		msw=0, ms=3, mc=:blue,
		label="Stable",
		xtick=0:0.1:2,
		xlabel="\$p\$",
		ylabel="\$x\$ at fixed point",
		title="Maasch & Saltzman system \$[70]\$"
	)
	scatter!(
		first.(unstable_fixed_points), getindex.(unstable_fixed_points, 2), 
		label="Unstable",
		msw=0, ms=3, mc=:red
	)
	vline!([0.96], label="Bifurcation: \$p=0.96\$", lc=:black)
	savefig("plots/exercise4_5.png")
end
